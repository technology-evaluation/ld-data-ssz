PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX qb: <http://purl.org/linked-data/cube#>
PREFIX ex: <http://example.org/>
PREFIX sh: <http://www.w3.org/ns/shacl#>
PREFIX ssz-schema: <https://ld.stadt-zuerich.ch/schema/>
PREFIX ssz-dimension: <https://ld.stadt-zuerich.ch/statistics/property/>

INSERT {

    ?dataSetUri a qb:DataSet ;
        rdfs:label ?viewLabel ;
        qb:structure ?dsdUri ;
        qb:slice ?sliceUri .

    # ?view is SliceKey as well, done in CSV mapping
    ?view qb:componentProperty ?dimensionUri  ;
        ssz-schema:viewStructure ?dsdUri .

    ?dsdUri a qb:DataStructureDefinition ;
        qb:component ?componentUri ;
        qb:sliceKey ?view .

    ?componentUri qb:dimension ?dimensionUri  ;
        qb:dimension ssz-dimension:RAUM  ;
        qb:dimension ssz-dimension:ZEIT  ;
        qb:measure ?measureUri  .

    ?sliceUri a qb:Slice ;
        qb:sliceStructure  ?view ;
        ?dimensionUri ?hardcodeUri .

} WHERE {
    {
        SELECT ?view ?viewLabel ?dataSetUri ?dimensionUri ?measureUri ?dsdUri ?sliceUri ?componentUri (COUNT(DISTINCT ?codes) AS ?count) WHERE {
            
            ?view a ssz-schema:View ;
                skos:notation ?viewNotation ;
                rdfs:label ?viewLabel ;
                <http://example.org/GESAMTCODE_RCS> ?gesammtcode .
                           
            ?obs a qb:Observation ;
                <http://example.org/GESAMTCODE_RCS> ?gesammtcode ;
                <http://example.org/KENNZAHL> ?kennzahl ;
                ( ex:GRUPPE1 | ex:GRUPPE2 | ex:GRUPPE3 | ex:GRUPPE4 | ex:GRUPPE5 ) ?gruppe .
                    
            FILTER(?gruppe != "XXX")
            BIND(URI(CONCAT("https://ld.stadt-zuerich.ch/statistics/dataset/", ?viewNotation)) AS ?dataSetUri)
            BIND(URI(CONCAT("https://ld.stadt-zuerich.ch/statistics/property/", ?gruppe)) AS ?dimensionUri)
            BIND(URI(CONCAT("https://ld.stadt-zuerich.ch/statistics/measure/", ?kennzahl)) AS ?measureUri)
            BIND(URI(CONCAT(CONCAT("https://ld.stadt-zuerich.ch/statistics/dataset/", ?viewNotation), "#structure")) AS ?dsdUri )
            BIND(BNODE(CONCAT('component_', $viewNotation)) AS ?componentUri )
            BIND(URI(CONCAT(CONCAT("https://ld.stadt-zuerich.ch/statistics/dataset/", ?viewNotation), "/slice")) AS ?sliceUri )
                    
            ?obs ?dimensionUri ?codes .

        } GROUP BY ?view ?viewLabel ?dataSetUri ?dimensionUri ?measureUri ?dsdUri ?sliceUri ?componentUri
    }
 
    { SELECT DISTINCT ?view ?dimensionUri ?lockedcode ?somecode {

        ?view a ssz-schema:View ;
            <http://example.org/GESAMTCODE_RCS> ?gesammtcode .
                            
            ?obs a qb:Observation ;
                <http://example.org/GESAMTCODE_RCS> ?gesammtcode ;
                ?dimensionUri ?somecode .
                           
        }
    }

    #BIND(IF(?count = 1, ?somecode, '') AS ?hardcodeUri)
    BIND(IF(?count = 1, ?somecode, URI("http://example.org/dummy")) AS ?hardcodeUri)
    # TODO if we filter like this we have dimensions missing for some reason!?
    #FILTER(?hardcodeUri != '')       
}
