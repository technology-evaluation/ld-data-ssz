PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX qb: <http://purl.org/linked-data/cube#>
PREFIX ex: <http://example.org/>
PREFIX sh: <http://www.w3.org/ns/shacl#>
PREFIX ssz-schema: <https://ld.stadt-zuerich.ch/schema/>
PREFIX ssz-dimension: <https://ld.stadt-zuerich.ch/statistics/property/>

# Important: This has to be run before qb-slices-code.rq. At least I think so, got way too many triples otherwise
INSERT {
    ?slice a ssz-schema:DefaultSlice ;
        rdfs:label ?label ;
        sh:shapesGraph ?shape .

}
 WHERE {

    ?dataset a qb:DataSet ;
        rdfs:label ?label ;
        skos:notation ?datasetNotation ;
        qb:slice ?slice .

    # no slice for these dataset(s)
    FILTER ( ?datasetNotation != "GEB-RAUM-ZEIT-NAF-NAM-SEX" )

    BIND(URI(REPLACE("https://stat.stadt-zuerich.ch/dataset/%%DATASET%%/slice", "%%DATASET%%" , ?datasetNotation)) AS ?target)

    ?shape a sh:NodeShape ;
        sh:targetNode ?target .

}