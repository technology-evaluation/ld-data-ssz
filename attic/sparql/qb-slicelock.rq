PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX qb: <http://purl.org/linked-data/cube#>
PREFIX ssz-schema: <https://ld.stadt-zuerich.ch/schema/>
PREFIX ssz-dimension: <https://ld.stadt-zuerich.ch/statistics/property/>

INSERT {
    # locked dimension to SliceKey
    ?sliceKey qb:componentProperty ?dimension .

    # Lock itself to Slilce
    ?slice ?dimension ?dimensionCode .
  
} WHERE {
{
    SELECT ?dataSet ?dimension ?sliceKey ?slice (COUNT(DISTINCT ?dimensionCode) AS ?count)  WHERE {
        ?dataSet a qb:DataSet ;
        skos:notation ?notation ;
        qb:structure/qb:component/qb:dimension ?dimension ;
          qb:structure/qb:sliceKey ?sliceKey ;
          qb:slice ?slice . 
  
        # no slice for these dataset(s)
        FILTER ( ?notation != "GEB-RAUM-ZEIT-NAF-NAM-SEX" )

        ?obs qb:dataSet ?dataSet ;
            ?dimension ?dimensionCode . 
      

    } GROUP BY ?dataSet ?dimension ?sliceKey ?slice  ORDER BY ?dataSet ?dimension
} 
FILTER( ?count = 1)
FILTER( ?dimension != ssz-dimension:ZEIT)
{
    SELECT DISTINCT ?dataSet ?dimension ?dimensionCode WHERE {
        ?obs qb:dataSet ?dataSet ;
            ?dimension ?dimensionCode . 
    } 
}
}